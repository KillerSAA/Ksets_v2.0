{$CLEO .csa}
{$NOSOURCE}
{$O 2003=-1,}
//booleans
int bNoDrunkEffect, bDriveWWeapons
int bNewMod, bExitCarFix, bAutoRemoveMarker, bHeadMove                            // Ksets by KillerSA
int ReturnValue, bReturned, bJPatch, bIniFiles

float fReturnValue

int RGBA[4]

define function GetWeatherType(): int
define function HeadMove(active: int)
define function ImprovedTreeMovement(WindForceInCl: float, WindForceInSt: float, WindForceInOw: float)

const
    // Func params
    R0 = 0, R1 = 1, R2 = 2
    R3 = 3, R4 = 4
    
    // addrs
    DETAILEDWATERDIST = 0x6B156C
    TheCamera = 0x952118
    // MakeNOP
    NOP_DWORD = 0x8000F3AF // For 4 bytes
    NOP_WORD = 0xBF00BF00 // For 2 bytes

    // Version
    GTA_SA_2_00 = 17
end


int GameVersion = get_game_version, StreamMem
if GameVersion <> GTA_SA_2_00
then
    end_thread
end

script_name 'Ksets'
0DD0: StreamMem = get_label_addr @_ZN10CStreaming18ms_memoryAvailableE
StreamMem = get_func_addr_by_cstr_name StreamMem

if 3A00: bIniFiles = aml_has_mod_loaded "net.alexblade.rusjj.inifiles"
then
else
    wait 10000 ms
    0AD1: show_formatted_text_highpriority "~r~[Error] you don't have the cleo ini files plugin installed!" time 8000
    end_thread
end
    
:Reload

    0AF0: bAutoRemoveMarker = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "AutoRemoveMarker"
    0AF0: bHeadMove = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "HeadMove"
    0AF0: ReturnValue = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "FixedCassinoLights"
    if ReturnValue == TRUE
    then
        write_mem_addr 0x6BD1E4 value 1 size 1 add_ib 1 protect 1
    else
        write_mem_addr 0x6BD1E4 value 0 size 1 add_ib 1 protect 1
    end
    
    0AF0: ReturnValue = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "No3dGunFlash"
    if ReturnValue == TRUE
    then
        write_mem_addr 0x4A4CCA value NOP_WORD size 4 add_ib 1 protect 1
        write_mem_addr 0x4A4CCE value NOP_WORD size 4 add_ib 1 protect 1
    else
        write_mem_addr 0x4A4CCA value 0xF4E9A129 size 4 add_ib 1 protect 1
        write_mem_addr 0x4A4CCE value 0xE000ECA8 size 4 add_ib 1 protect 1
    end
    
    0AF0: bNoDrunkEffect = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "NoDrunkEffect"
    0AF0: bDriveWWeapons = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "DriveWithoutWeapons"
    0AF0: ReturnValue = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "DisableShakeOnExplosions"
    if ReturnValue == TRUE
    then
        write_mem_addr 0x5D87A6 value NOP_DWORD size 4 add_ib 1 protect 1
    else
        write_mem_addr 0x5D87A6 value 0xE1B8F7BC size 4 add_ib 1 protect 1
    end
    
    0AF0: ReturnValue = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "DisableShakeSniper"
    if ReturnValue == TRUE
    then
        write_mem_addr 0x5DD8CE value NOP_DWORD size 4 add_ib 1 protect 1
    else
        write_mem_addr 0x5DD8CE value 0xE5C4F7BB size 4 add_ib 1 protect 1
    end
    0AF0: bExitCarFix = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "ExitCarFix"
    0AF0: ReturnValue = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "StreamMemory"
    if ReturnValue > 0
    then
        ReturnValue *= 1048576 // convert to bits
        write_mem_addr StreamMem value ReturnValue size 4 add_ib 0 protect 1
    end

    int bDisableBirds
    0AF0: bDisableBirds = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "DisableBirds"
    if bDisableBirds == TRUE
    then
        write_mem_addr 0x3F6350 value 0x8000F3AF size 4 add_ib 1 protect 1 // CBirds::Render(void)
    end
    
    // Skid color
    0AF0: RGBA[0] = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "R"
    0AF0: RGBA[1] = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "G"
    0AF0: RGBA[2] = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "B"
    0AF0: RGBA[3] = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "A"
    0AF0: ReturnValue = read_int_from_ini_file "configs/Ksets.ini" section "Configs" key "DETAILEDWATERDIST"
    
    3A00: bJPatch = aml_has_mod_loaded "net.rusjj.jpatch" // JPatch also changes this value, this will avoid problems
    if and
     ReturnValue > 0
     bJPatch == 0
    then
        write_mem_addr DETAILEDWATERDIST value ReturnValue size 4 add_ib 1 protect 1
    end
    
    // Improved tree movement
    float fWindForceInCl,fWindForceInSt, fWindForceInOw
    0AF2: fWindForceInCl = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "CloudyWeather"
    0AF2: fWindForceInSt = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "StormWeather"
    0AF2: fWindForceInOw = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "OtherWeathers"
    
    // Effects draw distance
    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "BoatSplashes"
    write_mem_addr 0x589D60 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "Blood"
    write_mem_addr 0x364118 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "Wood"
    write_mem_addr 0x3643B4 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "TyreBurst"
    write_mem_addr 0x364764 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "PunchImpact"
    write_mem_addr 0x364AF4 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "Debris"
    write_mem_addr 0x364D0C value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "Glass"
    write_mem_addr 0x364EFC value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelSpray"
    write_mem_addr 0x365250 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelGrass"
    write_mem_addr 0x3654E0 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelGravel"
    write_mem_addr 0x365760 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelMud"
    write_mem_addr 0x3659E8 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelSand"
    write_mem_addr 0x365D64 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelDust"
    write_mem_addr 0x3660F8 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "SingleWheelParticles"
    write_mem_addr 0x58B978 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "ExhaustParticles"
    write_mem_addr 0x58B428 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "TankFire"
    write_mem_addr 0x366544 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WaterSplash"
    write_mem_addr 0x3665D4 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "BulletSplash"
    write_mem_addr 0x366668 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "Gunshot"
    write_mem_addr 0x366348 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "HeliDust"
    write_mem_addr 0x560234 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "Sparks"
    write_mem_addr 0x364614 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "PedsLodDistance"
    write_mem_addr 0x5D6348 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "VehiclesLodDistance"
    write_mem_addr 0x5D6344 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "SunSize"
    write_mem_addr 0x5A4028 value fReturnValue size 4 add_ib 1 protect 1

    0AF2: fReturnValue = read_float_from_ini_file "configs/Ksets.ini" section "Configs" key "WheelTurningSpeed"
    write_mem_addr 0x55C8AC value fReturnValue size 4 add_ib 1 protect 1

    int bVar = true
    int blipp, gMobMenu, i, pSkidColor, CRGBA
    float x, y, z
    
    0DD0: CRGBA = get_label_addr @CRGBA
    CRGBA = get_func_addr_by_cstr_name CRGBA
    
    for i = 0 to 3 step 1
        switch i
            case 0
                0DD0: pSkidColor = get_label_addr @SKID_COLOR_DEFAULT
            case 1
                0DD0: pSkidColor = get_label_addr @SKID_COLOR_SAND
            case 2
                0DD0: pSkidColor = get_label_addr @SKID_COLOR_MUD
            case 3
                0DD0: pSkidColor = get_label_addr @SKID_COLOR_BLOOD
        end
        pSkidColor = get_func_addr_by_cstr_name pSkidColor

        set_register_value R0 pSkidColor
        set_register_value R1 RGBA[0]
        set_register_value R2 RGBA[1]
        set_register_value R3 RGBA[2]
        set_register_value R4 RGBA[3]
        context_call_func CRGBA
    end

while true // loop
    wait 0 ms 
    
    if has_game_just_returned_from_frontend
    then
        goto @Reload
    end
    
    ImprovedTreeMovement(fWindForceInCl,fWindForceInSt, fWindForceInOw)
    
    if bAutoRemoveMarker == TRUE
    then
        if 0AB6: store_target_marker_coords_to x y z
        then
            if 00FE:   actor $PLAYER_ACTOR sphere 0 in_sphere x y z radius 20.0 20.0 500.0
            then
                0DD0: gMobMenu = get_label_addr @gMobileMenu
                gMobMenu = get_func_addr_by_cstr_name gMobMenu // android
                gMobMenu += 0x48

                0DD8: blipp = read_mem_addr gMobMenu size 4 add_ib 0
                
                remove_blip blipp
                write_mem_addr gMobMenu value 0x0 size 4 add_ib 0 protect 0 // android
            end
        end
    end

    if bExitCarFix == TRUE
    then
        if is_player_playing $PLAYER_CHAR
        then
            if and
            not is_char_getting_in_to_a_car $PLAYER_ACTOR // onde esta o 09DE no pesquisar?
            is_char_in_any_car $PLAYER_ACTOR
            bVar == true
            then
                if is_widget_released 0
                then
                    task_leave_any_car $PLAYER_ACTOR
                end
                if is_widget_doubletapped 0
                then
                    bVar = false
                end
            end

            if bVar == false
            then
                if is_char_in_any_car $PLAYER_ACTOR
                then
                    task_leave_any_car $PLAYER_ACTOR
                else
                    bVar = true
                end
            end
        end
        bVar = true
    end

    HeadMove(bHeadMove)

    if bNoDrunkEffect == TRUE
    then
        set_player_drunkenness $PLAYER_CHAR drunk_visuals 0
    end
    
    if bDriveWWeapons == TRUE
    then 
        if and 
         is_player_playing $PLAYER_CHAR
         is_char_in_any_car $PLAYER_ACTOR
        then 
            06AB: set_actor $PLAYER_ACTOR all_weapons_hidden 1 
        end 

        if not is_char_in_any_car $PLAYER_ACTOR
        then 
            06AB: set_actor $PLAYER_ACTOR all_weapons_hidden 0 
        end
    end
    
end

function ImprovedTreeMovement(fWindForceInC: float, fWindForceInS: float, fWindForceInO: float)
    {$Include includes/eWeathersID.h}
    int iWeatherType = GetWeatherType()

    switch iWeatherType
        case WEATHER_CLOUDY_COUNTRYSIDE, WEATHER_CLOUDY_SF, WEATHER_CLOUDY_LS, WEATHER_CLOUDY_LV
            write_mem_addr 0x3ECF50 value fWindForceInC size 4 add_ib 1 protect 1
            
        case WEATHER_RAINY_COUNTRYSIDE, WEATHER_RAINY_SF, WEATHER_SANDSTORM
            write_mem_addr 0x3ECF50 value fWindForceInS size 4 add_ib 1 protect 1
        default
            write_mem_addr 0x3ECF50 value fWindForceInO size 4 add_ib 1 protect 1
    end
    
    cleo_return 0
end

function GetWeatherType(): int
    int addr, iType
    0DD0: addr = get_label_addr @_ZN8CWeather17ForcedWeatherTypeE
    addr = get_func_addr_by_cstr_name addr

    0DD8: iType = read_mem_addr addr size 1 add_ib 0

    cleo_return 1 iType
end

function HeadMove(bActive: int)
    float fCamX, fCamY, fCamZ, x, y, z
    float fOffX, fOffY, fOffZ
    float fActorX, fActorY, fActorZ
    float fVar, fDistance

    if and
      bActive == TRUE
      Player.IsPlaying($PLAYER_CHAR)
    then
        fCamX, fCamY, fCamZ = Camera.GetActivePointAt()
        04C4: store_coords_to fOffX fOffY fOffZ from_actor $PLAYER_ACTOR with_offset 0.0 0.0 0.0
        fOffX -= fCamX
        fOffY -= fCamY
        04C4: store_coords_to x y z from_actor $PLAYER_ACTOR with_offset fOffX fOffY fOffZ
        x *= fCamX
        y *= fCamY
        x *= 0.7
        y *= 0.7
        fOffX *= x
        fOffY *= y
        00A0: store_actor $PLAYER_ACTOR position_to fActorX fActorY fActorZ
        fVar = fCamZ
        fVar -= fActorZ
        0509: fDistance = distance_between_XY fActorX fActorY and_XY fOffX fOffY
        fVar *= fDistance
        fVar *= -1.0
        fCamZ += fVar
        fCamZ += fDistance
        Task.LookAtCoord($PLAYER_ACTOR, fOffX, fOffY, fCamZ, -1)
    end
    cleo_return 0
end

{script memory}

:_ZN8CWeather17ForcedWeatherTypeE
hex
    "_ZN8CWeather17ForcedWeatherTypeE" 00
end

:SKID_COLOR_BLOOD
hex
 "SKID_COLOR_BLOOD" 00
end

:SKID_COLOR_MUD
hex
 "SKID_COLOR_MUD" 00
end

:SKID_COLOR_DEFAULT //491
hex
 "SKID_COLOR_DEFAULT" 00
end

:SKID_COLOR_SAND
hex
 "SKID_COLOR_SAND" 00
end

:CRGBA
hex
 "_ZN5CRGBAC2Ehhhh" 00
end

:gMobileMenu
hex
 "gMobileMenu" 00
end

:_ZN10CStreaming18ms_memoryAvailableE
hex
 "_ZN10CStreaming18ms_memoryAvailableE" 00
end